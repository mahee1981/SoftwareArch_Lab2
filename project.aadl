package project
public
	with SEI;
	
--Definition of the system as a composite component
system AdaptiveCruiseControlSystem
	properties
		Reference_Processor => classifier(Real_Time.one_GHz);
		SEI::BandWidthCapacity => 50000.0 bitsps;
end AdaptiveCruiseControlSystem;



--Implementation of the system as speed control
	system implementation AdaptiveCruiseControlSystem.PBA_speed_control
	--the speed control is comprised of software and hardware components
	subcomponents
		speed_sensor:device sensor.speed; -- an instance of the speed sensor component in the system named speed_sensor (hardware)
		throttle: device actuator.speed; -- an instance of the speed actuator component in the system named throttle (hardware)
		speed_control: process control.speed; -- an instance of the process component in the system named speed control (software)
		interface_unit:device interface.pilot; -- an instance of the device component in the system named interface_unit (hardware)
		RT_1GHz: processor Real_Time.one_GHz; -- an instance of the processor component in the system named RT_1GHz (hardware)
    	Standard_Marine_Bus: bus Marine.Standard; -- an instance of the bus component in the system named Standard_Marine_bus (hardware)
    	Stand_Memory: memory RAM.Standard; -- an instance of the memory component in the system named Stand_Memory (hardware)
    	
    	-- added components for distance control
    	distance_sensor: device sensor.distance;
    	brake: device actuator.distance;
	connections
		
		
		-- DC1, DC2, and DC3 are port connections
		
		-- out port sensor_data from speed_sensor is connected to the in port speed_data in speed_control
		DC1: port speed_sensor.sensor_data -> speed_control.sensor_data{SEI::BandWidthBudget => 20.0 bitsps;}; 
		-- out port command_data from speed_control is connected to the in port cmd in throttle
		DC2: port speed_control.command_data -> throttle.cmd{SEI::BandWidthBudget => 20.0 bitsps;};
		-- out port set_speed from interface_unit is connected to the in port set_speed in speed_control
		DC3: port interface_unit.set_speed -> speed_control.set_speed{SEI::BandWidthBudget => 20.0 bitsps;};
		-- out port disengage from interface_unit is connected to the in port disengage in speed_control
		EC4: port interface_unit.disengage -> speed_control.disengage{SEI::BandWidthBudget => 20.0 bitsps;};
		
		
		-- extended connections for distance control
		DC4: port distance_sensor.sensor_data -> speed_control.sensor_data{SEI::BandWidthBudget => 20.0 bitsps;};
		DC5: port speed_control.command_data -> brake.cmd{SEI::BandWidthBudget => 20.0 bitsps;};
		DC6: port interface_unit.set_distance -> speed_control.set_distance{SEI::BandWidthBudget => 20.0 bitsps;};
		
		-- BAC1, BAC2, BAC3, BAC4, and BAC5 are the bus access connections for the components that need to communicate with the 
		-- PBA control software
		BAC1: bus access Standard_Marine_Bus <-> speed_sensor.BA1{SEI::BandWidthBudget => 20.0 bitsps;};
    	BAC2: bus access Standard_Marine_Bus <-> RT_1GHz.BA1{SEI::BandWidthBudget => 20.0 bitsps;};
    	BAC3: bus access Standard_Marine_Bus <-> throttle.BA1{SEI::BandWidthBudget => 20.0 bitsps;};
    	BAC4: bus access Standard_Marine_Bus <-> interface_unit.BA1{SEI::BandWidthBudget => 20.0 bitsps;};
    	BAC5: bus access Standard_Marine_Bus <-> Stand_Memory.BA1{SEI::BandWidthBudget => 20.0 bitsps;};
    	
    	-- extended bus accesses for distance control
    	BAC6: bus access Standard_Marine_Bus <-> distance_sensor.BA1{SEI::BandWidthBudget => 20.0 bitsps;};
    	BAC7: bus access Standard_Marine_Bus <-> brake.BA1{SEI::BandWidthBudget => 20.0 bitsps;};
    flows
		on_speed_end_to_end: end to end flow
		interface_unit.on_speed_flow_src -> DC3 ->
		speed_control.on_speed_flow_path -> DC2 ->
		throttle.on_speed_flow_snk {Latency => 180 ms .. 180 ms;};
		
		on_distance_end_to_end: end to end flow
		interface_unit.on_distance_flow_src -> DC6 ->
		speed_control.on_distance_flow_path -> DC5 ->
		brake.on_speed_flow_snk {Latency => 180 ms .. 180 ms;};
    	
    	
    	
    	
	properties
		-- allowed_processor_binding properties tie the execution of speed_control_laws and scale_speed_data threads to the processor RT_1GHz
	
		Actual_Processor_Binding => (reference(RT_1GHz))
    applies to speed_control.speed_control_laws;
  		Actual_Processor_Binding => (reference(RT_1GHz))
    applies to speed_control.scale_speed_data;
    
    
    	-- distance extension
  		Actual_Processor_Binding => (reference(RT_1GHz))
    applies to speed_control.scale_distance_data;
    
    
    	-- binds the storage of code of the speed_control process to the RAM 
  		Actual_Memory_Binding => (reference(Stand_Memory))
    applies to speed_control;

    	Actual_Connection_Binding => (reference (Standard_Marine_Bus))
    applies to DC1, DC2, DC3, DC4, DC5, DC6, EC4, BAC1, BAC2, BAC3, BAC4, BAC5, BAC6, BAC7;
    
end AdaptiveCruiseControlSystem.PBA_speed_control;


	

-- definition of a sensor component as a device
	device sensor
	features
		sensor_data: out data port;
		BA1: requires bus access Marine.Standard; -- communication of the component with the process requires access to the bus
	properties
		SEI::MIPSBudget => 0.05 MIPS;
		Period => 10ms;
		Latency => 500us .. 1ms;
	end sensor;

--implementation of the sensor component as distance sensor
	device implementation sensor.distance
end sensor.distance;

-- implementation of the sensor component as speed sensor
	device implementation sensor.speed	
end sensor.speed;
	
-- definition of an interface component as a device
	device interface
	features
		set_speed: out data port;
		set_distance: out data port; -- extended the functionality of the interface to include distance 
		disengage: out event port;
		BA1: requires bus access Marine.Standard; -- communication of the component with the process requires access to the bus
	flows
		on_speed_flow_src: flow source set_speed {latency => 5ms .. 5ms;};
		on_distance_flow_src: flow source set_distance{latency => 5ms .. 5ms;};
	properties
		SEI::MIPSBudget => 0.05 MIPS;
		Period => 10 ms;
		Latency => 500us .. 1ms;
	
	
	end interface;

-- implementation of the interface as pilot that will be used as an input for PBA information
	device implementation interface.pilot
end interface.pilot;

-- definition of a process component as control
	process control
	features
		command_data: out data port; -- data that gets sent to the actuator
		sensor_data: in data port; -- data received from the sensor
		set_speed: in data port; -- speed data received from the interface
		set_distance: in data port; -- extended functionality for distance setting;
		disengage: in event port; -- event port that receives the signal to disengage the PGA
	flows
		on_speed_flow_path: flow path set_speed -> command_data{latency => 10 ms .. 20 ms;};
		on_distance_flow_path: flow path set_distance -> command_data{latency => 10 ms .. 20 ms;};
		
	end control;
-- implementation of the control process as PBA control function
	process implementation control.speed
	-- the process is composed of two threads scale_speed_data and speed_control_laws
	subcomponents
		scale_speed_data: thread read_data.speed; -- thread that reads the sensor input data and performs scaling and filtering
    	speed_control_laws: thread control_laws.speed; -- thread that executes PBA speed control laws and sends output to throttle actuator
    	scale_distance_data: thread read_data.distance; -- thread that reads the distance input and performs scaling and filtering
		
  	connections
    	DC1: port sensor_data -> scale_speed_data.sensor_data; -- connection between the process in port and scale_speed_data thread in port
    	DC2: port scale_speed_data.proc_data -> speed_control_laws.proc_data; -- connection between the processed data of the scale_speed_data thread and speed_control_laws thread
    	DC3: port speed_control_laws.cmd -> command_data; -- connection of the output signal from the control_laws thread to the out port of
    	-- the process
    	EC1: port disengage -> speed_control_laws.disengage; -- event port connection between the process and speed_control_laws thread for
    	-- stopping the process
    	DC4: port set_speed -> speed_control_laws.set_speed; -- data port connection between the process and speed_control_laws thread for
    	-- setting the speed.
    	
    	-- extended functionalities for distance control
    	DC5: port sensor_data -> scale_distance_data.sensor_data;
    	DC6: port scale_distance_data.proc_data -> speed_control_laws.proc_data;
    	DC7: port set_distance -> speed_control_laws.set_speed;
		flows
			on_speed_flow_path: flow path set_speed -> DC4 -> speed_control_laws.set_speed_flow -> DC3 -> command_data{latency => 10 ms .. 20 ms;};
			on_distance_flow_path: flow path set_distance -> DC7 -> speed_control_laws.set_speed_flow -> DC3 -> command_data{latency => 10 ms .. 20 ms;}; 
			
		
    	
end control.speed;
	
-- definition of the actuator component as device
	device actuator
	features
		cmd:in data port; -- in data port for commands sent by the process
		BA1: requires bus access Marine.Standard; -- communication of the component with the process requires access to the bus
	flows
		on_speed_flow_snk: flow sink cmd{latency => 8 ms .. 8 ms;};
	properties
			SEI::MIPSBudget => 0.05 MIPS;
			Period => 10ms;
			Latency => 500us .. 1ms;
	end actuator;
-- implementation of the actuator device as a speed actuator used for modeling the throttle
	device implementation actuator.speed
end actuator.speed;


-- implementation of the actuator device as brake actuator used for modeling the brake
-- this serves as a distance actuator
	device implementation actuator.distance
end actuator.distance;

-- definition of the read_data software component as a thread
	thread read_data
  	features
    	sensor_data: in data port; 
    	proc_data: out data port;
		flows
			read_data_flow: flow path sensor_data -> proc_data;
  	properties
    	Dispatch_Protocol => Periodic;
    	Compute_Execution_Time => 1 ms .. 2 ms;
    	Period => 50 ms;
    	SEI::MIPSBudget => 0.2 MIPS;
end read_data;

-- implementation of the read_data thread as read_data.speed
	thread implementation read_data.speed
end read_data.speed;

-- implementation of the read_data thread as read_data.distance used for data processing
	thread implementation read_data.distance
end read_data.distance;

-- definition of the control_laws software component as a thread
	thread control_laws
  	features
    	proc_data: in data port;
    	cmd: out data port;
    	set_speed: in data port;
    	disengage: in event port;
		flows
			prod_data_flow: flow path proc_data -> cmd;
			set_speed_flow: flow path set_speed -> cmd;
		properties
			Dispatch_Protocol => Periodic;
    	Compute_Execution_Time => 3 ms .. 5 ms;
    	Period => 50 ms;
    	SEI::MIPSBudget => 0.5 MIPS;
 
end control_laws;

-- implementation of the control_laws thread as control_laws.speed
	thread implementation control_laws.speed
end control_laws.speed;

-- definition of a real_time component as a processor
	processor Real_Time
	features
		BA1: requires bus access Marine.Standard;  -- communication of the component with the process requires access to the bus
	properties	
		Scheduling_Protocol => (RMS);
		SEI::MIPSCapacity => 10.0 MIPS;
end Real_Time;

-- implementation of a real_time processor as real_time processor of 1 GHz
processor implementation Real_Time.one_GHz
end Real_Time.one_GHz;
-- definition of a memory component as RAM
	memory RAM
  	features
    	BA1: requires bus access Marine.Standard; -- communication of the component with the process requires access to the bu
	end RAM;
-- implementation of RAM memory as standard RAM memory
	memory implementation RAM.Standard
end RAM.Standard;
-- definition of Marine Bus component as bus
	bus Marine
end Marine;
-- implementation of a marine bus as standard marine bus component
	bus implementation Marine.Standard
		properties
			SEI::BandWidthCapacity => 500000.0 bitsps;
    		Transmission_Time => [fixed  => 1ms .. 3ms; ];
    		Latency => 1ms .. 2ms;  -- acquistion time
    		
    		
end Marine.Standard;




end project;